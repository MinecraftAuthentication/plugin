buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

version = "1.16.5"
group = "me.minecraftauth"
archivesBaseName = "${modid}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    // snapshot   YYYYMMDD   Snapshot are built nightly.
    // stable     #          Stables are built at the discretion of the MCP team.
    // official   MCVersion  Official field/method names from Mojang mapping files
    mappings channel: "official", version: "1.16.5"
    //makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        server {
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            workingDirectory project.file('run')
            arg "-mixin.config="+archivesBaseName+".mixins.json"

            property 'forge.logging.console.level', 'debug'

            mods {
                minecraftauth {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories{
    mavenCentral()
}

dependencies {
    shaded project(':common')
    minecraft "net.minecraftforge:forge:1.16.5-36.2.0"
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

jar {
    archivesBaseName = 'MinecraftAuth-Forge'

    manifest {
        attributes([
                "Specification-Title": "${modid}",
                "Specification-Vendor": "minecraftauth",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"minecraftauth",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${modid}.mixins.json"
        ])
    }
}

apply plugin: 'org.spongepowered.mixin'
mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

// ensure shadowJar gets re-obfuscated
tasks.shadowJar.dependsOn "reobfJar"
reobf { shadowJar {} }
